plugins {
    id 'org.springframework.boot' version '3.2.4'
}

apply plugin: 'java-library'
apply plugin: 'jacoco'

ext {
    jiaVersion = '1.1.1-SNAPSHOT'
}

dependencies {
    implementation "cn.jia:jia-common-starter:$jiaVersion"
    implementation "cn.jia:jia-base-mapper:$jiaVersion"
    implementation "cn.jia:jia-dwz-mapper:$jiaVersion"
    implementation "cn.jia:jia-isp-mapper:$jiaVersion"
    implementation "cn.jia:jia-kefu-mapper:$jiaVersion"
    implementation "cn.jia:jia-material-mapper:$jiaVersion"
    implementation "cn.jia:jia-oauth-mapper:$jiaVersion"
    implementation "cn.jia:jia-oauth-resource:$jiaVersion"
    implementation "cn.jia:jia-point-mapper:$jiaVersion"
    implementation "cn.jia:jia-sms-mapper:$jiaVersion"
    implementation "cn.jia:jia-task-mapper:$jiaVersion"
    implementation "cn.jia:jia-user-mapper:$jiaVersion"
    implementation "cn.jia:jia-wx-mapper:$jiaVersion"
    implementation "cn.jia:jia-mcp-client:$jiaVersion"

    implementation(libs.spring.boot.starter.redisson)
    implementation(libs.spring.boot.starter.thymeleaf)
    implementation(libs.spring.boot.starter.security)
    implementation(libs.spring.boot.starter.data.elasticsearch)

    implementation libs.slf4j.api
    implementation libs.jakarta.inject.api
    implementation libs.jakarta.annotation.api
    implementation libs.mapstruct.core
    implementation libs.jodd

    compileOnly 'com.intellij:annotations:+@jar'
    annotationProcessor libs.lombok
    annotationProcessor libs.mapstruct.processor
    compileOnly libs.lombok

    testImplementation "cn.jia:jia-common-test:$jiaVersion"
    testAnnotationProcessor libs.lombok
    testCompileOnly libs.lombok
}

group = 'cn.jia'
version = '1.1.1-SNAPSHOT'
description = 'cyf-api-kit'

sourceCompatibility = JavaVersion.VERSION_21

tasks.withType(JavaCompile).tap {
    configureEach {
        options.encoding = 'UTF-8'
    }
}

// 禁用普通 JAR
jar {
    enabled = false
}

bootJar {
    manifest {
        attributes 'Start-Class': 'cn.jia.JiaApplication' // Spring Boot 主类
    }
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test // 确保先运行测试
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
    // 多模块项目需聚合子模块报告
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/config/**',
                    '**/entity/**',
                    '**/dto/**'
            ])
        }))
    }
}